{
id: "juliet",
is_human: true,
is_strict: true,
hitpoints: 1,

friction: 2000,
traction: 1400,
traction_in_air: 30,

solid_dimensions: ["player", "common"],

properties: {
	process_in_air: "def() ->commands [

		if(is_standing, set(animation, 'idle')),

		if(ctrl_right, [
			set(facing, 1),
			set(accel_x, 1500),
		]),

		if(ctrl_left, [
			set(facing, -1),
			set(accel_x, 1500),
		]),

		if(ctrl_tongue and can_cling, [
			set(animation, 'wall_cling'),
			set(y, desired_ypos_when_clinging),
		]),
	]",

	//If Juliet can cling to the wall.
	can_cling: "bool ::
	(
		(not point_solid(level, me, xpoint, solid_rect.y)) and
		point_solid(level, me, xpoint, int(solid_rect.y*0.95+solid_rect.y2*0.05))
	)
		where xpoint = if(facing > 0, solid_rect.x2+1, solid_rect.x-1)
	",

	desired_ypos_when_clinging: "int ::
	if(result = null, top_y, top_y + result)
	    where result = find(range(bot_y - top_y), point_solid(level, me, xpoint, top_y + value))
		where top_y = solid_rect.y
		where bot_y = int(solid_rect.y*0.95+solid_rect.y2*0.05)
		where xpoint = if(facing > 0, solid_rect.x2+1, solid_rect.x-1)
	",
},

on_process_wall_cling: "[
	if(not ctrl_tongue, set(animation, 'fall')),

	if(ctrl_up, [
		set(mid_xy, [mid_x + facing*solid_rect.w, mid_y - solid_rect.h - 64]),

		move_to_standing(),

		set(animation, 'idle'),
	]),
]",

on_enter_wall_cling_anim: "[
	set(velocity_y, 0),
	set(accel_y, 0),
]",

on_end_idle_anim: "set(animation, 'idle')",
on_end_anim: "set(animation, animation)",

on_create: "set(animation, 'idle')",

on_process: "[]",


on_process_walk: "[
	if(not (ctrl_right or ctrl_left), set(animation, 'idle')),
]",


on_process_idle: "[
	if(ctrl_left, [
		set(animation, 'walk'),
		set(facing, -1),
	]),
	
	if(ctrl_right, [
		set(animation, 'walk'),
		set(facing, 1)
	]),

	if(not is_standing, set(animation, 'fall')),
]",

on_process_jump: "[
	if(velocity_y > 100, [
		set(animation, 'fall'),
	]),

	process_in_air(),
]",

on_process_fall: "[
	process_in_air(),
]",

on_ctrl_jump: "[
	if(is_standing, [
		set(velocity_y, -3000),
		set(animation, 'jump'),
	]),
]",

solid_area: [74, 31, 104, 170],

animation: [
	{
	id: "idle",
	image_pattern: "sprites/juliet/juliet_idle*.png",
	accel_x: 0,
	accel_y: 80,
	body_area: "all",
	feet_width: 5,
	duration: 5,
	reverse: true,
	scale: 1,
	},

	{
	id: "walk",
	image_pattern: "sprites/juliet/juliet_running*.png",
	accel_x: 1000,
	accel_y: 80,
	body_area: "all",
	feet_width: 5,
	duration: 5,
	scale: 1,
	reverse: true,
	},


	{
	id: "jump",
	image_pattern: "sprites/juliet/juliet_jumping*.png",
	accel_x: 0,
	accel_y: 80,
	body_area: "all",
	feet_width: 5,
	duration: 5,
	scale: 1,
	reverse: true,
	},


	{
	id: "fall",
	image_pattern: "sprites/juliet/juliet_falling*.png",
	accel_x: 0,
	accel_y: 80,
	body_area: "all",
	feet_width: 5,
	duration: 5,
	scale: 1,
	reverse: true,
	},


	{
	id: "wall_cling",
	image_pattern: "sprites/juliet/juliet_grabbing*.png",
	accel_x: 0,
	accel_y: 0,
	body_area: "all",
	feet_width: 5,
	duration: 5,
	scale: 1,
	reverse: true,
	},
],


}
